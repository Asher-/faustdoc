# Faust 101 for the confined

The covid-19 containment offers us at least one excellent opportunity to train! If you've always wanted to get into Faust programming but didn't have the time, here's your chance! 

The objective of this workshop is to familiarize you with the Faust language through very simple examples of signal processing and sound synthesis.
The documentation and the examples we will use can be found here: [https://faustdoc.grame.fr](https://faustdoc.grame.fr). All examples will be run in the online Faust IDE [https://faustide.grame.fr](https://faustide.grame.fr). If ever the sounds produced with the IDE are of poor quality, with some clicks, one can use the online editor, which is more rustic, but also lighter [https://fausteditor.grame.fr](https://fausteditor.grame.fr).

## Super simple examples

Let start with super simple examples.

### The simplest Faust program
This is the simplest Faust program imaginable. It contains only one line of code, the definition: `process = _;`.

<!-- faust-run -->
```
process = _;
```
<!-- /faust-run -->

Several lessons can be learned from this very simple example:

- a Faust program has at least one definition, that of the keyword `process` which indicates the entry point of the program. 
- A definition always ends with a `;`. A common mistake is to forget the semicolon at the end of a definition.
- the `_` sign represents one of the _primitives_ of the language (primitives are the predefined functions of the language). It represents an _audio cable_ that lets the signal pass through without transforming it. This is what one calls in mathematics the _identity_ function, the signal which enters on the left comes out on the right identically. 

Lets try this program using the online Faust IDE...

### Adding two signals

We saw in the previous example the primitive `_`. Faust has a large number of primitives, including all mathematical operations (see ).

The `+` primitive for instance is used to add two signals. It can therefore be used to transform a stereophonic signal (on two channels) into a monophonic signal as in the following example:


<!-- faust-run -->
```
process = +;
```
<!-- /faust-run -->


### Multiplying two signals

The `*` primitive for instance is used to multiply two signals. 

<!-- faust-run -->
```
process = *;
```
<!-- /faust-run -->

As you can hear, multiplying the two channels of a signal between them transforms the sound quite drastically.


### Parallel composition

Programming in Faust consists in assembling primitive operations to form more or less complex audio circuits. To realize these assemblies Faust has 5 composition operations: `~`, `,`, `;`, `<:`, `:>`.  

Let's first look at the parallel composition represented by the comma `,`. 


<!-- faust-run -->
```
process = _, _;
```
<!-- /faust-run -->

We made a _stereo cable_ and when we play the audio file we now hear it on both speakers.

It is very important to distinguish between _primitives_, such as `_`, `+` or `*`, and _composition operations_ such as `,` or `;`.  _Primitives_ represent operations on audio signals, whereas composition operations are used to link two audio operations together. In other words, you can write `+` or `*` alone, because they represent valid audio operations, but you can never write `,` or `:` alone because they are used to connect two audio operations. You must always write `A,B` or `A:B`.


Here is a summary table of the five composition operators:

| Syntax   | Priority  | Association  | Description             |
| -------- | --------- | ------------ | ----------------------- |
| `A ~ B`  | 4         | left         | Recursive Composition   |
| `A , B`  | 3         | right        | Parallel Composition    |
| `A : B`  | 2         | right        | Sequential Composition  |
| `A <: B` | 1         | right        | Split Composition       |
| `A :> B` | 1         | right        | Merge Composition       |



### Controlling the volume

Let's see an example where we combine three primitives:
  `_`, `0.1` and `*`,
with two composition operators:
`,` and `:`.

The idea here is to lower the volume of the incoming signal to one tenth of its initial value. This is done by multiplying the incoming signal by `0.1`:

<!-- faust-run -->
```
process = (_, 0.1) : *; // try to replace 0.1 by other values between 0 and 1
```
<!-- /faust-run -->

Note that we have used parentheses in this example to clearly mark the order in which things should be done. We start by putting `_` and `0.1` in parallel, and then compose them in sequence with `*`.

But, just as in `(2*3)+7` were the parentheses are not really necessary because multiplication takes precedence over addition, one could write directly `process = _,0.1 : *;` without the parentheses, because parallel composing takes precedence over sequential composing. The priority of the composition operators is shown in the previous table.


### Controlling the volume with a slider

Instead of controlling the volume by editing the code, it is far more convenient to use a graphical slider. For that purpose we can use a `hslider(...)`, a horizontal slider. It takes five parameters. The first one is the name `"volume"`, then we have the defaut value `0.1`, the mimimun value `0`, the maximum value `1` and a step value `0.1`. So here the default value is `0.1` 

<!-- faust-run -->
```
process = _, hslider("volume", 0.1, 0, 1, 0.01) : *;
```
<!-- /faust-run -->

### Mono Amplifier

So far we have written very simple programs that fit into one line of code. We will now introduce additional definitions. A definition should be understood as a way of giving a name to something, which saves us from typing the definition every time and makes the program easier to understand.
 

<!-- faust-run -->
```
monoamp = _, hslider("volume", 0.1, 0, 1, 0.01) : *;
process = monoamp;
```
<!-- /faust-run -->



### Stereo Amplifier
 
Continuing in the same vein, we will define a stereo amplifier as two mono amplifiers in parallel.

<!-- faust-run -->
```
monoamp = _, hslider("volume", 0.1, 0, 1, 0.01) : *;
stereoamp = monoamp,monoamp;

process = stereoamp;
```
<!-- /faust-run -->

Note that even if the hslider volume appears several times in our code, there will only be one in the user interface.



### Vertical sliders

Instead of horizontal sliders, we could use vertical sliders. Just replace hslider(...) with vslider(...).

<!-- faust-run -->
```
monoamp = _, vslider("volume", 0.1, 0, 1, 0.01) : *;
stereoamp = monoamp,monoamp;

process = stereoamp;
```
<!-- /faust-run -->



### Knobs instead of sliders

By default sliders are ... sliders! You can change their appearance by using the metadata mechanism. 

Metadata is information that you put in square brackets in the slider name. For example the metadata "...[style:knob]..." allows you to turn the slider into a rotary knob.

<!-- faust-run -->
```
monoamp = _, vslider("volume[style:knob]", 0.1, 0, 1, 0.01) : *;

stereoamp = monoamp,monoamp;

process = stereoamp;
```
<!-- /faust-run -->


### Syntactic sugar

So far we used the core syntax of Faust. For example to multiply the incomming signal by `0.1` we wrote `_,0.1:*`. For numerical expressions this notation is not always the most convenient and sometime we would prefer to use the more traditional infix notation and write instead `_*0.1`. We can also use the prefixed notation and write `*(0.1)`. 

Let's rewrite the definition of the monoamp using the prefix notation:

<!-- faust-run -->
```
monoamp = *(vslider("volume[style:knob]", 0.1, 0, 1, 0.01));

stereoamp = monoamp,monoamp;

process = stereoamp;
```
<!-- /faust-run -->

Here is a table of equivalent notations. Keep in mind that infix and prefix expressions are translated to core syntax.



Here is a table with the same expression in all three syntaxes.

| Expression | Description             |
| ---------- | ----------------------- |
| `_,0.1:*`  | core syntax             |
| `_*0.1`    | infix notation          |
| `*(0.1)`   | prefix notation         |

These notations can be freely combined. For example, the following expressions are all equivalent:

| Expression | Description             |
| ------------- | ----------------------- |
| `*(1-m)`      | prefix + infix notation |
| `_*(1-m)`     | only infix notation     |
| `_,(1,m:-):*` | core syntax             |



### A _mute_ button

We would like to be able to mute the sound completely at the touch of a button, without having to change the volume. 

Let's add a mute stage to our mono amplifier. In order to mute the signal we just have to mutiply it by 0. We will use for that purpose a `checkbox(...)`, a user interface element that produces a signal which is 0 by default and 1 when it is checked. As we want to multiply the signal by 0 when the checkbox is checked we will use `1-checkbox ("mute")`.

<!-- faust-run -->
```
mute = *(1-checkbox("mute"));

monoamp = *(vslider("volume[style:knob]", 0.1, 0, 1, 0.01)) : mute;

stereoamp = monoamp,monoamp;

process = stereoamp;
```
<!-- /faust-run -->



### Vertical and horizontal Layout

As can be seen in the previous example, by default, the layout of the elements is vertical. You can change this layout by using  `hgroup(...)` and  `vgroup(...)`. For example to make the layout horizontal you can write:

<!-- faust-run -->
```
mute = *(1-checkbox("mute"));

monoamp = *(vslider("volume[style:knob]", 0.1, 0, 1, 0.01)) : mute;

stereoamp = hgroup("Marshall", monoamp,monoamp);

process = stereoamp;
```
<!-- /faust-run -->



### Differentiate the volume of the two channels

To differentiate the volume control of our two channels, we will parametrize monoamp with a channel number `c` which will be used to differentiate the name of each volume control. Note that the name `c` of the parameter must only have one letter to be well interpreted in the slider name `"volume %c[style:knob]"`.

<!-- faust-run -->
```
mute = *(1-checkbox("mute"));

monoamp(c) = *(vslider("volume %c[style:knob]", 0.1, 0, 1, 0.01)) : mute;

stereoamp = hgroup("Marshall", monoamp(0),monoamp(1));

process = stereoamp;
```
<!-- /faust-run -->




### Having many channels

We have built a stereo amp, but suppose we wanted to generalize this construction to an arbitrary number of channels. To do so, we will instroduce the `par(i, N, ...)` construction which allows us to put several times an expression in parallel. It is in a way the equivalent of the for() loop of a classical programming language. 

In our case we want to indicate the number of channels of our amplifier.


<!-- faust-run -->
```
mute = *(1-checkbox("mute"));

monoamp(c) = *(vslider("volume %c[style:knob]", 0.1, 0, 1, 0.01)) : mute;

multiamp(N) = hgroup("Marshall", par(i, N, monoamp(i)));

process = multiamp(2); // try multiamp(4)
```
<!-- /faust-run -->


## Delays and Feedbacks

In this new section we will see two important notions, that of delay with the `@` primitive, and that of feedback (from a looped circuit) which will require the use of the recursive composition `A~B` which allows to loop the outputs of A into the inputs of B and the outputs of B into the inputs of A.

### Monophonic delay of 1 second

Let's start with a very simple example, a monophonic delay of  1 seconds or 44100 samples. We will use the prefix notation :


<!-- faust-run -->
```
process = @(44100); 

// equivalent to core syntax
// process = _, 44100 ; @;
```
<!-- /faust-run -->


### Delay of 0.1 second on the right channel

To hear the delay better, let's put it only on the right channel and leave the left channel unchanged.

<!-- faust-run -->
```
process = _, @(4410); 
```
<!-- /faust-run -->


### Stereo Delay with freeze



<!-- faust-run -->
```
monoecho(d,f) = *(g) : + ~  (@(d):*(f)) 
    with {
        l = 0.95;
        g = 1 - max(0, f-l)/(1-l);
    };

stereoecho(d,f) = monoecho(d,f),monoecho(d,f);

stereoecho_demo = stereoecho(44100/4, hslider("feedback", 0, 0, 1, 0.01));

process = stereoecho_demo; 
```
<!-- /faust-run -->

